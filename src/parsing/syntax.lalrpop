/// This is the parser we have. I am by no means comfortable writing parsing
/// code like this, but I do think that this code is better and more
/// maintainable than if I had written the parser by hand.
use crate::{
  arena::Arena,
  ast::{Pattern, Expr, Stmt, Binop, Unop, Prog, FunDec},
  builtins::Function,
  common::{FileSpec, Either},
  runtime::{strtoi,strtod,hextoi},
  lexer::{self, Tok},
};
use lalrpop_util::ParseError;

grammar<'a, 'outer>(
  arena: &'a Arena<'outer>,
  buf: &mut Vec<u8>,
  prog: &mut Prog<'a, 'a, &'a str>,
);

pub VarDef: (&'a str, &'a Expr<'a, 'a, &'a str>) = {
    <id:"IDENT"> "=" <e:BaseTerm> => (id, e),
}

ToplevelBase: () = {
   <Begin> =>? {
     if prog.begin.is_some() {
       Err(ParseError::User{ error: "Only one BEGIN block allowed".into() })
     } else {
       prog.begin = Some(<>);
       Ok(())
     }
   },

   <End> =>? {
     if prog.end.is_some() {
       Err(ParseError::User{ error: "Only one END block allowed".into() })
     } else {
       prog.end = Some(<>);
       Ok(())
     }
   },

   <Prepare> =>? {
     if prog.prepare.is_some() {
       Err(ParseError::User{ error: "Only one PREPARE block allowed".into() })
     } else {
       prog.prepare = Some(<>);
       Ok(())
     }
   },

   <Function> => prog.decs.push(<>),
}

ToplevelBraced: () = {
  ToplevelBase,
  <PatAction> => prog.pats.push(<>),
}

UnbracedPattern: () = {
  <Expr> "\n"+ => prog.pats.push((Pattern::Bool(<>), None)),
  <e1:BaseTerm> "," <e2:BaseTerm> "\n"+ => prog.pats.push((Pattern::Comma(e1, e2), None)),
}

ProgInner: () = {
  UnbracedPattern,
  ToplevelBraced,
  ProgInner ToplevelBraced,
  ProgInner UnbracedPattern,
}

pub Prog: () = {
  "\n"* ProgInner?
}

Function: FunDec<'a, 'a, &'a str> = {
    <name:"FUNDEC"> "(" <args:FormalParams?> Rparen <body:Block> =>
        FunDec {
          name,
          body,
          args: args.unwrap_or(Default::default()),
       }
}

FormalParams: Vec<&'a str> = {
   <"IDENT"> => vec![<>],
   <v:(<"IDENT"> ",")+> <iopt:(<"IDENT">)?> => match iopt {
      Some(e) => { let mut v = v; v.push(e); v }
      None => v,
   }
}

Begin: &'a Stmt<'a,'a,&'a str> = {
    "BEGIN" "\n"* <Block> => <>
}

Prepare: &'a Stmt<'a,'a,&'a str> = {
    "PREPARE" "\n"* <Block> => <>
}

End: &'a Stmt<'a,'a,&'a str> = {
    "END" "\n"* <Block> => <>
}

PatAction: (Pattern<'a,'a,&'a str>, Option<&'a Stmt<'a,'a,&'a str>>) = {
  <p:Expr?> <b:Block> => (match p {
                   Some(e) => Pattern::Bool(e),
                   None => Pattern::Null,
              }, Some(b)),
  <l:BaseTerm> "," <r:BaseTerm> <b:Block> => (Pattern::Comma(l, r), Some(b)),
}

// Resolving if/else groupings courtesy of wikipedia

Stmt: &'a Stmt<'a, 'a, &'a str> = {
    OpenStmt,
    ClosedStmt,
}

OpenStmt: &'a Stmt<'a,'a,&'a str> = {
    "if" "(" <cond:Expr> Rparen <s1:ClosedStmt> Else <s2:OpenStmt> => arena.alloc_v(Stmt::If(cond, s1, Some(s2))),
    "if" "(" <cond:Expr> Rparen <s1:Stmt> => arena.alloc_v(Stmt::If(cond, s1, None)),
    "while" "(" <cond:Expr> Rparen <body:OpenStmt> => arena.alloc_v(Stmt::While(false, cond, body)),
    "for" "(" <init: ExprNoIn?> ";" <cond:Expr?> ";" <update:Expr?> Rparen <body:OpenStmt> =>
        arena.alloc_v(Stmt::For(
                init.map(|x| arena.alloc_v(Stmt::Expr(x))),
                cond,
                update.map(|x| arena.alloc_v(Stmt::Expr(x))),
                body
        )),
    // TODO: for ((i, j) in arr)
    "for" "(" <id:"IDENT"> "in" <arr:Expr> Rparen <body:OpenStmt> =>
        arena.alloc_v(Stmt::ForEach(id, arr, body)),
}

ClosedStmt: &'a Stmt<'a,'a,&'a str> = {
    BaseStmt,
    "if" "(" <cond:Expr> Rparen <s1:ClosedStmt> Else <s2:ClosedStmt> =>
           arena.alloc_v(Stmt::If(cond, s1, Some(s2))),
    "while" "(" <cond:Expr> Rparen <body:ClosedStmt> => arena.alloc_v(Stmt::While(false, cond, body)),
    "for" "(" <init: ExprNoIn?> ";" <cond:Expr?> ";" <update:Expr?> Rparen <body:ClosedLoopBody> =>
        arena.alloc_v(Stmt::For(
                init.map(|x| arena.alloc_v(Stmt::Expr(x))),
                cond,
                update.map(|x| arena.alloc_v(Stmt::Expr(x))),
                body
        )),
    "for" "(" <id:"IDENT"> "in" <arr:Expr> Rparen <body:ClosedLoopBody> =>
        arena.alloc_v(Stmt::ForEach(id, arr, body)),


    Do <body:BaseStmt> "while" "(" <cond:Expr> ")" Sep => arena.alloc_v(Stmt::DoWhile(cond, body)),
}

Getline : &'a Expr<'a, 'a, &'a str> = {
    "getline" <into:BaseTerm?> <from:("<" <Expr>)?> =>
      arena.alloc_v(Expr::Getline{into, from, is_file: true}),
    <from:PrecFieldRef> "|" "getline" <into:BaseTerm?> =>
      arena.alloc_v(Expr::Getline{into, from: Some(from), is_file: false}),
}

Redirect: (&'a Expr<'a, 'a, &'a str>, FileSpec) = {
    ">" <Expr> => (<>, FileSpec::Trunc),
    ">>" <Expr> => (<>, FileSpec::Append),
    "|" <Expr> => (<>, FileSpec::Cmd),
}

ClosedLoopBody: &'a Stmt<'a, 'a, &'a str> = {
    SemiSep => arena.alloc_v(Stmt::Block(vec![])),
    ClosedStmt,
}

BaseStmt: &'a Stmt<'a, 'a, &'a str> = {
   <LeafStmt> Sep => <>,
   Block,
}

LeafStmt: &'a Stmt<'a, 'a, &'a str> = {
    <e: Expr> => arena.alloc_v(Stmt::Expr(e)),
    "delete" <i: IndexBase> =>
        arena.alloc_v(Stmt::Expr(arena.alloc_v(Expr::Call(Either::Right(Function::Delete), vec![i.0, i.1])))),
    "print" <pa:PrintArgs?> <re:Redirect?> =>
        arena.alloc_v(Stmt::Print(pa.unwrap_or(Vec::new()), re)),
    "print(" "\n"* <pa:(<Args?>)> ")" <re:Redirect?> =>
        arena.alloc_v(Stmt::Print(pa.unwrap_or(Vec::new()), re)),
    "printf" <spec:PrecAdd> <pa: ("," "\n"* <PrintArgs>)?> <re:Redirect?> =>
        arena.alloc_v(Stmt::Printf(spec, pa.unwrap_or(Vec::new()), re)),
    "printf(" "\n"* <spec:(<Expr> "\n"*)> <pa: ("," "\n"* <Args>)?> ")" <re:Redirect?> =>
        arena.alloc_v(Stmt::Printf(spec, pa.unwrap_or(Vec::new()), re)),
    "break" => arena.alloc_v(Stmt::Break),
    "continue" => arena.alloc_v(Stmt::Continue),
    "next" => arena.alloc_v(Stmt::Next),
    "nextfile" => arena.alloc_v(Stmt::NextFile),
    "return" <Expr?> => arena.alloc_v(Stmt::Return(<>)),
}

Block: &'a Stmt<'a,'a,&'a str> = {
    Lbrace Rbrace SemiSep? => arena.alloc_v(Stmt::Block(vec![])),
    Lbrace <LeafStmt> Rbrace SemiSep? => <>,
    Lbrace <BlockInner> Rbrace SemiSep? => arena.alloc_v(Stmt::Block(<>)),
}

BlockInner: Vec<&'a Stmt<'a,'a,&'a str>> = {
    <v:(<Stmt>)+> <e:LeafStmt?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
}

PrintArgs: Vec<&'a Expr<'a,'a,&'a str>> = {
    // To avoid ambiguities with expressions including ">" we jump down the precedence hierarchy
    // past the comparison operators.
    <PrecAdd> => vec![<>],
    <v:(<PrecAdd> "," "\n"*)+> <e:PrecAdd> => { let mut v = v; v.push(e); v },
}

Args: Vec<&'a Expr<'a,'a,&'a str>> = {
    // To avoid ambiguities with expressions including ">" we jump down the precedence hierarchy
    // past the comparison operators.
    <Expr> => vec![<>],
    <v:(<Expr> "," "\n"*)+> <e:(<Expr> "\n"*)?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
}

Expr: &'a Expr<'a,'a,&'a str> = {
    Getline,
    PrecAsgn
};

PrecAsgn: &'a Expr<'a,'a,&'a str> = {
    <l: PrecIn> "=" <r: PrecAsgn> => arena.alloc_v(Expr::Assign(l, r)),
    <l: PrecIn> "+=" <r: PrecAsgn> => arena.alloc_v(Expr::AssignOp(l, Binop::Plus, r)),
    <l: PrecIn> "-=" <r: PrecAsgn> => arena.alloc_v(Expr::AssignOp(l, Binop::Minus, r)),
    <l: PrecIn> "*=" <r: PrecAsgn> => arena.alloc_v(Expr::AssignOp(l, Binop::Mult, r)),
    <l: PrecIn> "/=" <r: PrecAsgn> => arena.alloc_v(Expr::AssignOp(l, Binop::Div, r)),
    <l: PrecIn> "^=" <r: PrecAsgn> => arena.alloc_v(Expr::AssignOp(l, Binop::Pow, r)),
    <l: PrecIn> "%=" <r: PrecAsgn> => arena.alloc_v(Expr::AssignOp(l, Binop::Mod, r)),
    PrecTern,
}

LookupList: &'a Expr<'a, 'a, &'a str> = {
    <first:PrecMatch> <rest:("," <PrecMatch>)+> => {
      let mut res = first;
      let subsep = arena.alloc_v(Expr::Var("SUBSEP"));
      for dim in rest.into_iter() {
        res = arena.alloc_v(Expr::Binop(Binop::Concat, res, subsep));
        res = arena.alloc_v(Expr::Binop(Binop::Concat, res, dim));
      }
      res
   }
}

PrecTern: &'a Expr<'a, 'a, &'a str> = {
   <c: PrecOr> "?" <t: PrecTern> ":" <f: PrecTern> => arena.alloc_v(Expr::ITE(c, t, f)),
   PrecOr,
}

PrecOr: &'a Expr<'a, 'a, &'a str> = {
    <l: PrecAnd> Or <r: PrecOr> => arena.alloc_v(Expr::Or(l, r)),
    PrecAnd,
}

PrecAnd: &'a Expr<'a, 'a, &'a str> = {
    <l: PrecIn> And <r: PrecAnd> => arena.alloc_v(Expr::And(l, r)),
    PrecIn,
}

PrecIn: &'a Expr<'a,'a,&'a str> = {
    <l: PrecMatch> "in" <r: PrecMatch> =>
        arena.alloc_v(Expr::Call(Either::Right(Function::Contains), vec![r, l])),
    "(" <l: LookupList> Rparen "in" <r: PrecMatch> =>
        arena.alloc_v(Expr::Call(Either::Right(Function::Contains), vec![r, l])),
    PrecMatch,
}

PrecMatch: &'a Expr<'a,'a,&'a str> = {
    <l: PrecMatch> "~" <r: PrecCmp> => arena.alloc_v(Expr::Binop(Binop::IsMatch, l, r)),
    <l: PrecMatch> "!~" <r: PrecCmp> => arena.alloc_v(Expr::Unop(
            Unop::Not,
            arena.alloc_v(Expr::Binop(Binop::IsMatch, l, r)))),
    PrecCmp,
}

// XXX Replicate the first two layers of the precedence hierarchy to skip "in" expressions to avoid
// ambiguity between beginning of for loop and foreach loop. This is a hack; we should find a way
// to tell LALRPOP the right thing here.
ExprNoIn: &'a Expr<'a,'a,&'a str> = {
    Getline,
    PrecAsgnNoIn
};

PrecAsgnNoIn: &'a Expr<'a,'a,&'a str> = {
    <l: PrecTernNoIn> "=" <r: PrecAsgn> => arena.alloc_v(Expr::Assign(l, r)),
    <l: PrecTernNoIn> "+=" <r: PrecAsgn> => arena.alloc_v(Expr::AssignOp(l, Binop::Plus, r)),
    <l: PrecTernNoIn> "-=" <r: PrecAsgn> => arena.alloc_v(Expr::AssignOp(l, Binop::Minus, r)),
    <l: PrecTernNoIn> "*=" <r: PrecAsgn> => arena.alloc_v(Expr::AssignOp(l, Binop::Mult, r)),
    <l: PrecTernNoIn> "/=" <r: PrecAsgn> => arena.alloc_v(Expr::AssignOp(l, Binop::Div, r)),
    <l: PrecTernNoIn> "^=" <r: PrecAsgn> => arena.alloc_v(Expr::AssignOp(l, Binop::Pow, r)),
    <l: PrecTernNoIn> "%=" <r: PrecAsgn> => arena.alloc_v(Expr::AssignOp(l, Binop::Mod, r)),
    PrecTernNoIn,
}

PrecTernNoIn: &'a Expr<'a, 'a, &'a str> = {
   <c: PrecOrNoIn> "?" <t: PrecTernNoIn> ":" <f: PrecTern> => arena.alloc_v(Expr::ITE(c, t, f)),
   PrecOrNoIn,
}

PrecOrNoIn: &'a Expr<'a, 'a, &'a str> = {
    <l: PrecAndNoIn> Or <r: PrecOrNoIn> => arena.alloc_v(Expr::Or(l, r)),
    PrecAndNoIn,
}

PrecAndNoIn: &'a Expr<'a, 'a, &'a str> = {
    <l: PrecMatch> And <r: PrecAndNoIn> => arena.alloc_v(Expr::And(l, r)),
    PrecMatch,
}


PrecCmp: &'a Expr<'a,'a,&'a str> = {
    <l: PrecAdd> "<" <r: PrecCmp> => arena.alloc_v(Expr::Binop(Binop::LT, l, r)),
    <l: PrecAdd> "<=" <r: PrecCmp> => arena.alloc_v(Expr::Binop(Binop::LTE, l, r)),
    <l: PrecAdd> ">" <r: PrecCmp> => arena.alloc_v(Expr::Binop(Binop::GT, l, r)),
    <l: PrecAdd> ">=" <r: PrecCmp> => arena.alloc_v(Expr::Binop(Binop::GTE, l, r)),
    <l: PrecAdd> "==" <r: PrecCmp> => arena.alloc_v(Expr::Binop(Binop::EQ, l, r)),
    <l: PrecAdd> "!=" <r: PrecCmp> => arena.alloc_v(Expr::Unop(Unop::Not, arena.alloc_v(Expr::Binop(Binop::EQ, l, r)))),
    PrecAdd
}

PrecAdd: &'a Expr<'a,'a,&'a str> = {
    <l: PrecAdd> "+" <r:PrecMul>  => arena.alloc_v(Expr::Binop(Binop::Plus, l, r)),
    <l: PrecAdd> "-" <r:PrecMul>  => arena.alloc_v(Expr::Binop(Binop::Minus, l, r)),
    PrecMul,
}

PrecMul: &'a Expr<'a,'a,&'a str> = {
    <l: PrecMul> "*" <r:PrecPow> => arena.alloc_v(Expr::Binop(Binop::Mult, l, r)),
    <l: PrecMul> "/" <r:PrecPow> => arena.alloc_v(Expr::Binop(Binop::Div, l, r)),
    <l: PrecMul> "%" <r:PrecPow> => arena.alloc_v(Expr::Binop(Binop::Mod, l, r)),
    PrecPow,
}

PrecPow: &'a Expr<'a, 'a, &'a str> = {
    <l: PrecUnop> "^" <r: PrecPow> => arena.alloc_v(Expr::Binop(Binop::Pow, l, r)),
    PrecUnop
}

PrecUnop: &'a Expr<'a,'a,&'a str> = {
    "-" <e: PrecInc> => arena.alloc_v(Expr::Unop(Unop::Neg, e)),
    "+" <e: PrecInc> => arena.alloc_v(Expr::Unop(Unop::Pos, e)),
    "!" <e: PrecInc> => arena.alloc_v(Expr::Unop(Unop::Not, e)),
    PrecInc
}

PrecInc: &'a Expr<'a,'a,&'a str> = {
    <e:CatBaseTerm>"++" => arena.alloc_v(Expr::Inc { is_inc: true, is_post: true, x: e }),
    <e:CatBaseTerm>"--"=> arena.alloc_v(Expr::Inc { is_inc: false, is_post: true, x: e }),
    "++" <e:CatBaseTerm> => arena.alloc_v(Expr::Inc { is_inc: true, is_post: false, x: e }),
    "--" <e:CatBaseTerm> => arena.alloc_v(Expr::Inc { is_inc: false, is_post: false, x: e }),
    CatBaseTerm,
}

CatBaseTerm: &'a Expr<'a,'a, &'a str> = {
    <l: CatBaseTerm> <r: PrecFieldRef> => arena.alloc_v(Expr::Binop(Binop::Concat, l, r)),
    PrecFieldRef
}

PrecFieldRef: &'a Expr<'a,'a,&'a str> = {
    Col,
    BaseTerm
}

Col: &'a Expr<'a,'a,&'a str> = {
    "$" <e:BaseTerm> => arena.alloc_v(Expr::Unop(Unop::Column, e)),
}

Ident: &'a Expr<'a,'a,&'a str> = {
  "IDENT" => arena.alloc_v(Expr::Var(arena.alloc_str(<>))),
}

StrLit: &'a Expr<'a,'a,&'a str> = {
  "STRLIT" => arena.alloc_v(Expr::StrLit(lexer::parse_string_literal(<>, &arena, buf))),
}

Index: &'a Expr<'a,'a,&'a str> = {
    <i:IndexBase> => arena.alloc_v(Expr::Index(i.0, i.1)),
}

IndexBase: (&'a Expr<'a,'a,&'a str>, &'a Expr<'a,'a,&'a str>) = {
  <arr:BaseTerm> "[" <e:Expr> "]" => (arr, e),
  <arr:BaseTerm> "[" <ll:LookupList> "]" => (arr, ll),
}

BaseTerm: &'a Expr<'a,'a, &'a str> = {
  LeafTerm,
  "(" <e:Expr> ")" => e,
}

LeafTerm: &'a Expr<'a,'a, &'a str> = {
  Ident,
  Index,
  StrLit,
  "INT" => arena.alloc_v(Expr::ILit(strtoi(<>.as_bytes()))),
  "HEX" => arena.alloc_v(Expr::ILit(hextoi(<>.as_bytes()))),
  "FLOAT" => arena.alloc_v(Expr::FLit(strtod(<>.as_bytes()))),
  "PATLIT" => arena.alloc_v(Expr::PatLit(lexer::parse_regex_literal(<>, &arena, buf))),
  // TODO: not Rparen for these next two?
  <i:CallStart> <args:Args?> ")" =>
        arena.alloc_v(Expr::Call(Either::Left(i), args.unwrap_or(vec![]))),
}



And: () = { "&&" "\n"* }
Or: () = { "||" "\n"* }
Do: () = { "do" "\n"* }
Else: () = { "else" "\n"* }
Lbrace: () = { "{" "\n"* }
Rbrace: () = { "}" "\n"* }
#[inline]
Rparen: () = { ")" "\n"* }
SemiSep: () = { ";" "\n"* }
Sep: () = { "\n"+, ";" "\n"* }
CallStart: &'a str = {
   <"CALLSTART"> "\n"*
}

extern {
  type Location = lexer::Loc;
  type Error = lexer::Error;
  enum Tok<'a> {
      "INT" => Tok::ILit(<&'a str>),
      "HEX" => Tok::HexLit(<&'a str>),
      "FLOAT" => Tok::FLit(<&'a str>),
      "IDENT" => Tok::Ident(<&'a str>),
      "STRLIT" => Tok::StrLit(<&'a str>),
      "PATLIT" => Tok::PatLit(<&'a str>),
      "CALLSTART" => Tok::CallStart(<&'a str>),
      "FUNDEC" => Tok::FunDec(<&'a str>),
      "BEGIN" =>  Tok::Begin,
      "PREPARE" => Tok::Prepare,
      "END" =>  Tok::End,
      "break" =>  Tok::Break,
      "continue" =>  Tok::Continue,
      "next" =>  Tok::Next,
      "nextfile" =>  Tok::NextFile,
      "for" =>  Tok::For,
      "if" =>  Tok::If,
      "else" => Tok::Else,
      "print" =>  Tok::Print,
      "print(" =>  Tok::PrintLP,
      "printf" =>  Tok::Printf,
      "printf(" =>  Tok::PrintfLP,
      "while" =>  Tok::While,
      "do" =>  Tok::Do,
      "{" =>  Tok::LBrace,
      "}" =>  Tok::RBrace,
      "[" =>  Tok::LBrack,
      "]" =>  Tok::RBrack,
      "(" =>  Tok::LParen,
      ")" =>  Tok::RParen,
      "getline" =>  Tok::Getline,
      "|" => Tok::Pipe,
      "=" =>  Tok::Assign,
      "+" =>  Tok::Add,
      "+=" =>  Tok::AddAssign,
      "-" =>  Tok::Sub,
      "-=" =>  Tok::SubAssign,
      "*" =>  Tok::Mul,
      "*=" =>  Tok::MulAssign,
      "/" =>  Tok::Div,
      "/=" =>  Tok::DivAssign,
      "^" =>  Tok::Pow,
      "^=" =>  Tok::PowAssign,
      "%" =>  Tok::Mod,
      "%=" =>  Tok::ModAssign,
      "~" =>  Tok::Match,
      "!~" =>  Tok::NotMatch,
      "==" =>  Tok::EQ,
      "!=" =>  Tok::NEQ,
      "<" =>  Tok::LT,
      "<=" =>  Tok::LTE,
      ">" =>  Tok::GT,
      "--" =>  Tok::Decr,
      "++" =>  Tok::Incr,
      ">=" =>  Tok::GTE,
      ">>" =>  Tok::Append,
      ";" =>  Tok::Semi,
      "$" => Tok::Dollar,
      "\n" =>  Tok::Newline,
      "," =>  Tok::Comma,
      "!" => Tok::Not,
      "||" => Tok::OR,
      "&&" => Tok::AND,
      "?" => Tok::QUESTION,
      ":" => Tok::COLON,
      "delete" => Tok::Delete,
      "in" => Tok::In,
      "return" => Tok::Return,
  }
}
